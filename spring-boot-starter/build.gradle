plugins {
  id 'buildlogic.java-library-conventions'
  id 'tech.medivh.plugin.publisher' version '1.2.5'
}

dependencies {
  compileOnly project(':spring')
  compileOnly 'org.springframework.boot:spring-boot-autoconfigure'
  compileOnly 'org.springframework.boot:spring-boot-starter'
  api("jakarta.annotation:jakarta.annotation-api:3.0.0")
  compileOnly 'org.smartbit4all.platform:api'
  compileOnly 'org.springframework.boot:spring-boot-starter-web'

  compileOnly 'org.smartbit4all.platform:sql'
  compileOnly 'org.springframework.boot:spring-boot-starter-jdbc'
}

jar {
  from(
    project(':common').sourceSets.main.output,
    project(':core').sourceSets.main.output,
    project(':spring').sourceSets.main.output,
    project(':arc-script').sourceSets.main.output // FIXME: Put ArcScript in separate lib!
  )
}

def pfr = tasks.register('processFrontendResources', Copy) {
  def src = file("${project.rootDir}/app-angular/dist/app-angular/browser")
  def dest = file("${project.rootDir}/spring-boot-starter/build/resources/main/storage-explorer")

  group = 'Frontend'
  description = 'Process Frontend Resources'
  dependsOn project(':app-angular').tasks.named('assembleFrontend')

  println("From $src to $dest")

  from src
  into dest
}

tasks.named('processResources') {
  dependsOn('processFrontendResources')
}

medivhPublisher {
  groupId = 'com.aestallon.storageexplorer'
  artifactId = 'storageexplorer-spring-boot-starter'
  version = '0.4.1'

  pom {
    name = "Storage Explorer Spring Boot Starter"
    description = "Spring Boot starter to serve a RESTful API for an embedded smartbit4all storage explorer"
    url = "https://github.com/szabolcs-bazil-papp/storage-explorer"
    licenses {
      license {
        name = "GPL-3.0 license"
        url = "https://www.gnu.org/licenses/gpl-3.0.txt"
      }
    }
    developers {
      developer {
        id = "bazilp"
        name = "Szabolcs Bazil Papp"
        email = "papp.szabolcs.bazil@gmail.com"
      }
    }
    scm {
      connection = "scm:git:"
      url = "https://github.com/szabolcs-bazil-papp/storage-explorer"
    }
  }
}
