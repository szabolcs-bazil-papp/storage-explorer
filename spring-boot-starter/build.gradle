plugins {
  id 'java-library'
  id 'io.spring.dependency-management' version '1.1.6'
  id 'maven-publish'
}

group = 'hu.aestallon.storageexplorer'
version = '0.2.0'

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(21))
  }

  withSourcesJar()
  withJavadocJar()
}

repositories {
  mavenCentral()
}

dependencyManagement {
  imports {
    mavenBom 'org.springframework.boot:spring-boot-dependencies:3.3.3'
  }
}

dependencies {
  api project(':spring')
  compileOnly 'org.springframework.boot:spring-boot-autoconfigure'
  compileOnly 'org.springframework.boot:spring-boot-starter'
  api("jakarta.annotation:jakarta.annotation-api:3.0.0")
  compileOnly 'org.smartbit4all.platform:api'
  compileOnly 'org.springframework.boot:spring-boot-starter-web'

  compileOnly 'org.smartbit4all.platform:sql'
  compileOnly 'org.springframework.boot:spring-boot-starter-jdbc'
}

jar {
  from(
    project(':common').sourceSets.main.output,
    project(':core').sourceSets.main.output,
    project(':spring').sourceSets.main.output
  )
}

test {
  useJUnitPlatform()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = 'hu.aestallon.storageexplorer'
      artifactId = 'storageexplorer-spring-boot-starter'
      version = '0.2.0'
      from components.java
      pom {
        name = 'Storage Explorer SpringBoot Starter'
        description = 'Enables a RESTful API for introspecting the embedded smartbit4all storage'
        url = 'https://aestallon.com'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'bazilp'
            name = 'Szabolcs Bazil Papp'
            email = 'szabolcs.papp@it4all.hu'
          }
        }
      }
    }
  }
}